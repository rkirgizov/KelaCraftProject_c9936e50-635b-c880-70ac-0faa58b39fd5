Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
// -----------------------------------------------------------------------------------
// UPGRADE ON UPGRADE TOOLS
// -----------------------------------------------------------------------------------
//REGION Use tool UPGRADE 

// Creation mod block use whetstone
IF
UseStarted(_User,_Tool)
AND
IsControlled(_User, 1)
AND
HasActiveStatus(_User, "KELA_CRAFT_CREATION_MOD", 1)
AND
DB_KELA_UpgradeTools_Tags(_Tag, _TagName)
AND
IsTagged(_Tool, _Tag, 1)
AND
_Tag == KELAWHETSTONE_80b11843-186f-4b78-bd17-850d5c7ec4d9
THEN
PROC_PlayCantUseItemAD((CHARACTER)_User);
SetDualEntityEvent(_User, _User, "Creation mod block use whetstone");


// Forging mod block use whetstone
IF
UseStarted(_User,_Tool)
AND
IsControlled(_User, 1)
AND
HasActiveStatus(_User, "KELA_CRAFT_REFORGING", 1)
AND
DB_KELA_UpgradeTools_Tags(_Tag, _TagName)
AND
IsTagged(_Tool, _Tag, 1)
AND
_Tag == KELAWHETSTONE_80b11843-186f-4b78-bd17-850d5c7ec4d9
THEN
PROC_PlayCantUseItemAD((CHARACTER)_User);
SetDualEntityEvent(_User, _User, "Forging mod block use whetstone");


// NOT PASS Check Upgrade in Process
IF
UseStarted(_User,_Tool)
AND
IsControlled(_User, 1)
AND
NOT HasActiveStatus(_User, "KELA_CRAFT_CREATION_MOD", 1)
AND
NOT HasActiveStatus(_User, "KELA_CRAFT_REFORGING", 1)
AND
DB_KELA_UpgradeTools_Tags(_Tag, _TagName)
AND
IsTagged(_Tool, _Tag, 1)
AND
Concatenate("KELA_MESSAGE_UPGRADE_IN_PROCESS_",_TagName,_InProcessStatus)
AND
HasActiveStatus(_Tool, _InProcessStatus, 1)
THEN
PROC_PlayCantUseItemAD((CHARACTER)_User);
// SetDualEntityEvent(_User, _User, "NOT PASS Check Upgrade in Process");

// Tool has UpgradeTool
IF
UseStarted(_User,_Tool)
AND
IsControlled(_User, 1)
AND
NOT HasActiveStatus(_User, "KELA_CRAFT_CREATION_MOD", 1)
AND
NOT HasActiveStatus(_User, "KELA_CRAFT_REFORGING", 1)
AND
DB_KELA_UpgradeTools_Tags(_Tag, _TagName)
AND
IsTagged(_Tool, _Tag, 1)
AND
GetItemByTagInInventory("KELAWEAPONUPGRADETOOL_4c6fe143-48cc-4613-9da3-2ed62440c902", _Tool, _)
THEN
SetDualEntityEvent(_User, _Tool, "KELA_UpgradingStarted");

// Tool has not UpgradeTool
IF
UseStarted(_User,_Tool)
AND
IsControlled(_User, 1)
AND
NOT HasActiveStatus(_User, "KELA_CRAFT_CREATION_MOD", 1)
AND
NOT HasActiveStatus(_User, "KELA_CRAFT_REFORGING", 1)
AND
DB_KELA_UpgradeTools_Tags(_Tag, _TagName)
AND
IsTagged(_Tool, _Tag, 1)
AND
NOT GetItemByTagInInventory("KELAWEAPONUPGRADETOOL_4c6fe143-48cc-4613-9da3-2ed62440c902", _Tool, _)
THEN
TemplateAddTo(KELA_Helper_Invisible_WeaponUpgradeTool_73b91f98-59ef-475d-b356-d6141db7c7af,_Tool,1,0);
DB_KELA_Upgrade_Tool_User(_Tool,_User);
TimerLaunch("KELA_Wait_For_Adding_UpgradeTool", 100);

// Wait for adding UpgradeTool
IF
TimerFinished("KELA_Wait_For_Adding_UpgradeTool")
AND
DB_KELA_Upgrade_Tool_User(_Tool,_User)
THEN
SetDualEntityEvent(_User, _Tool, "KELA_UpgradingStarted");
TimerLaunch("KELA_Wait_Tool_User_Clear", 200);

IF
TimerFinished("KELA_Wait_Tool_User_Clear")
AND
DB_KELA_Upgrade_Tool_User(_Tool,_User)
THEN
NOT DB_KELA_Upgrade_Tool_User(_Tool,_User);


// PASS Check Upgrade in Process with Oil Owner
IF
DualEntityEvent(_User, _Tool, "KELA_UpgradingStarted")
AND
DB_KELA_UpgradeTools_Tags(_Tag, _TagName)
AND
IsTagged(_Tool, _Tag, 1)
AND
GetItemByTagInInventory("KELAWEAPONUPGRADETOOL_4c6fe143-48cc-4613-9da3-2ed62440c902", _Tool, _UpgradeTool)
AND
Concatenate("KELA_MESSAGE_UPGRADE_IN_PROCESS_",_TagName,_InProcessStatus)
AND
NOT HasActiveStatus(_Tool, _InProcessStatus, 1)
AND
GetLevel(_User,_Level)
AND
IntegerToReal(_Level,_LevelRealValue)
AND
//GetItemByTagInPartyInventory("KELASMITHOIL_b45cf712-6f66-4cf0-ab52-c8baeb27f8c6", (CHARACTER)_User, (ITEM)_Oil)
GetItemByTemplateInPartyInventory((ITEMROOT)GRN_GreaseBottle_A_e2968a2e-bc83-4d50-99ef-39ce78d2d630,(CHARACTER)_User,(ITEM)_Oil)
AND
GetInventoryOwner(_Oil, _OilOwner)
THEN
SetDualEntityEvent(_UpgradeTool, _Tool, _TagName); // set coretool and coretooltag
SetEntityEventReal(_UpgradeTool,"KELA_SetUserLevel",_LevelRealValue); // set user level
SetDualEntityEvent(_UpgradeTool, _OilOwner, "KELA_SetOwnerOfSmithOil"); // set oil owner
SetDualEntityEvent(_User, _UpgradeTool, "KELA_CheckProficiencyWithWeapon");
Use((CHARACTER)_User, (ITEM)_UpgradeTool, "");

// PASS Check Upgrade in Process Without Oil Owner, but with status KELA_MESSAGE_PLAYER_HAS_NOT_SMITH_OIL
IF
DualEntityEvent(_User, _Tool, "KELA_UpgradingStarted")
AND
DB_KELA_UpgradeTools_Tags(_Tag, _TagName)
AND
IsTagged(_Tool, _Tag, 1)
AND
GetItemByTagInInventory("KELAWEAPONUPGRADETOOL_4c6fe143-48cc-4613-9da3-2ed62440c902", _Tool, _UpgradeTool)
AND
Concatenate("KELA_MESSAGE_UPGRADE_IN_PROCESS_",_TagName,_InProcessStatus)
AND
NOT HasActiveStatus(_Tool, _InProcessStatus, 1)
AND
GetLevel(_User,_Level)
AND
IntegerToReal(_Level,_LevelRealValue)
AND
//NOT GetItemByTagInPartyInventory("KELASMITHOIL_b45cf712-6f66-4cf0-ab52-c8baeb27f8c6", (CHARACTER)_User, _)
NOT GetItemByTemplateInPartyInventory((ITEMROOT)GRN_GreaseBottle_A_e2968a2e-bc83-4d50-99ef-39ce78d2d630,(CHARACTER)_User,_)
THEN
SetDualEntityEvent(_UpgradeTool, _Tool, _TagName); // set coretool and coretooltag
SetEntityEventReal(_UpgradeTool,"KELA_SetUserLevel",_LevelRealValue); // set user level
SetDualEntityEvent(_UpgradeTool, NULL_00000000-0000-0000-0000-000000000000, "KELA_SetOwnerOfSmithOil"); // set NULL oil owner
SetDualEntityEvent(_User, _UpgradeTool, "KELA_CheckProficiencyWithWeapon"); // check proficiency
Use((CHARACTER)_User, (ITEM)_UpgradeTool, "");

// ----------------------------------------------------------------
// EXTRA CHECKS
// ----------------------------------------------------------------
//
// Check Proficiency With Weapon
IF
DualEntityEvent(_User, _UpgradeTool, "KELA_CheckProficiencyWithWeapon")
AND
GetEquippedWeapon((CHARACTER)_User, _Weapon)
AND
IsProficientWith(_User, _Weapon, 1)
THEN
SetEntityEventReal(_UpgradeTool,"KELA_SetProficiencyWithWeapon",1.0);
// ELSE
IF
DualEntityEvent(_User, _UpgradeTool, "KELA_CheckProficiencyWithWeapon")
AND
GetEquippedWeapon((CHARACTER)_User, _Weapon)
AND
NOT IsProficientWith(_User, _Weapon, 1)
THEN
SetEntityEventReal(_UpgradeTool,"KELA_SetProficiencyWithWeapon",0.0);
// ----------------------------------------------------------------

// ----------------------------------------------------------------
// ROLL RESULTS
// RollResult((STRING)_EventName, (CHARACTER)_Roller, (GUIDSTRING)_RollSubject, (INTEGER)_ResultType, (INTEGER)_IsActiveRoll, (CRITICALITYTYPE)_Criticality)
// ----------------------------------------------------------------

// NOT Critical
IF
RollResult("KELA_Upgrading_RollEvent", _User, _RollSubject, _ResultType, _, _Criticality)
AND
NOT _Criticality == CRITICALITYTYPE.Success
AND
NOT _Criticality == CRITICALITYTYPE.Fail
AND
IntegerToReal(_ResultType,_ResultTypeRealValue)
THEN
SetEntityEventReal(_RollSubject,"KELA_RollResultCriticalNot",_ResultTypeRealValue);

// Critical Success
IF
RollResult("KELA_Upgrading_RollEvent", _User, _RollSubject, _ResultType, _, _Criticality)
AND
_Criticality == CRITICALITYTYPE.Success
AND
IntegerToReal(_ResultType,_ResultTypeRealValue)
THEN
SetEntityEventReal(_RollSubject,"KELA_RollResultCriticalSuccess",_ResultTypeRealValue);

// Critical Fail
IF
RollResult("KELA_Upgrading_RollEvent", _User, _RollSubject, _ResultType, _, _Criticality)
AND
_Criticality == CRITICALITYTYPE.Fail
AND
IntegerToReal(_ResultType,_ResultTypeRealValue)
THEN
SetEntityEventReal(_RollSubject,"KELA_RollResultCriticalFail",_ResultTypeRealValue);

// ----------------------------------------------------------------
// HANDLINGS
// ----------------------------------------------------------------

// Need apply effect on tool
IF
EntityEvent(_Tool,"KELA_Apply_Craft_Effect")
THEN
PlayEffect(_Tool,(EFFECTRESOURCE)VFX_Status_MagicWeapon_ApplyFX_01_1a366c75-a573-78e0-e667-1f615703fcc7);

// Oil used
IF
EntityEvent(_User,"KELA_Blacksmiths_Oil_Used")
THEN
//TemplateRemoveFrom((ITEMROOT)LOOT_KELA_Smith_Oil_3486ab6b-a396-4f39-a5cf-16dee0f873fd, _User, 1);
TemplateRemoveFrom((ITEMROOT)GRN_GreaseBottle_A_e2968a2e-bc83-4d50-99ef-39ce78d2d630, _User, 1);

// Because for the item in the inventory the timer in Anubis does not end
// Timer before request roll
IF
EntityEvent(_Item,"KELA_Start_Upgrade_Timer")
THEN
ObjectTimerLaunch(_Item, "KELA_Upgrade_Timer", 2500);
IF
ObjectTimerFinished(_Item, "KELA_Upgrade_Timer")
THEN
SetEntityEvent(_Item,"KELA_Stop_Upgrade_Timer");
//-------------------------------------------------
// Timer for completing status calculations
IF
EntityEvent(_Item,"KELA_Start_ApplyStatus_Timer")
THEN
ObjectTimerLaunch(_Item, "KELA_ApplyStatus_Timer", 500);
IF
ObjectTimerFinished(_Item, "KELA_ApplyStatus_Timer")
THEN
SetEntityEvent(_Item,"KELA_Stop_ApplyStatus_Timer");
//-------------------------------------------------

//END_REGION
// -----------------------------------------------------------------------------------
// UPGRADE ON UPGRADE TOOLS
// -----------------------------------------------------------------------------------
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "GLO_KelaCraftProject"
